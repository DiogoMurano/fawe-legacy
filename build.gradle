plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2" apply false
}

def releaseVersion = findProperty("github.tag") ?: getCommit()
println("Version: ${releaseVersion}")

String getCommit() {
    def stdout = new ByteArrayOutputStream()

    exec {
        commandLine "git", "rev-parse", "--short", "HEAD"
        standardOutput = stdout
    }

    return stdout.toString().trim()
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'

    apply plugin: 'maven-publish'

    group = 'io.github.whilein.fawe-legacy'
    version = releaseVersion

    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        options.encoding = "UTF-8"
    }

    repositories {
        mavenCentral()
        // worldguard
        maven { url "https://maven.enginehub.org/repo/" }
        // protocollib
        maven { url "https://repo.dmulloy2.net/content/groups/public/" }
        // paper-api
        maven { url "https://repo.destroystokyo.com/repository/maven-public/" }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }

        repositories {
            mavenLocal()

            // github packages
            maven {
                url = "https://maven.pkg.github.com/whilein/fawe-legacy"

                credentials {
                    username = findProperty("gpr.user")
                    password = findProperty("gpr.password")
                }
            }
        }
    }
}